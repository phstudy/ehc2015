apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'

mainClassName = "org.phstudy.ItemCount"

sourceCompatibility = 1.6

compileJava {
    targetCompatibility = 1.6
}

repositories {
    mavenCentral();

    maven {
        url "https://repository.cloudera.com/artifactory/cloudera-repos/"
    }
}

jar {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
}

dependencies {
	compile 'commons-daemon:commons-daemon:1.0.13'
    compile 'commons-httpclient:commons-httpclient:3.1'
    compile 'org.apache.hadoop:hadoop-client:2.0.0-cdh4.7.1'
    compile 'org.apache.hadoop:hadoop-mapreduce-examples:2.0.0-cdh4.7.1'
    //compile 'org.apache.mahout:mahout-integration:0.9'
}

task hadoop(dependsOn: jar, type: Exec) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        project.ext.HADOOP_HOME = "$System.env.HADOOP_HOME".replaceAll('\\\\', '/')
        project.ext.HADOOP_CMD = HADOOP_HOME + '/bin/hadoop.cmd'
        commandLine 'cmd', '/c', HADOOP_CMD, 'jar', "$jar.archivePath"
    } 
    else {
        project.ext.HADOOP_HOME = "/usr/lib/hadoop"
        project.ext.HADOOP_CMD = HADOOP_HOME + '/bin/hadoop'
        commandLine HADOOP_CMD, 'jar', "$jar.archivePath"
    }

    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
        args.each { commandLine.push(it) }      
    }

    standardOutput = new ByteArrayOutputStream()
    
    project.ext.output = {
        return standardOutput.toString()
    }
}
